buildscript {
    ext.gdxVersion = '1.10.1-SNAPSHOT'
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.1.15'
        classpath 'org.gretty:gretty:3.0.5'
        classpath 'com.android.tools.build:gradle:7.1.0-alpha13'
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

task texturePacker {
    def input = "$projectDir/raw-img"
    def outputDir = "$projectDir/android/assets/img"
    def outputName = 'skin'
    inputs.dir input
    outputs.file "$outputDir/${outputName}.atlas"
    doLast {
        com.badlogic.gdx.tools.texturepacker.TexturePacker.process(
            input,
            outputDir,
            outputName,
        )
    }
}

task generateResources {
    dependsOn texturePacker
}

allprojects {
    apply plugin: "eclipse"
    version = '0.1'
    ext {
        appName = "Unstable Connection"
        roboVMVersion = '2.3.12'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.3'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.1.0'
    }
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

subprojects {
    tasks.all {
        dependsOn generateResources
    }
}

project(":desktop") {
    apply plugin: "java-library"
    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "com.android.application"
    configurations { natives }
    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    }
}

project(":html") {
    apply plugin: "java-library"
    apply plugin: "gwt"
    apply plugin: "war"
    apply plugin: "org.gretty"
    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        api "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        api "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        
    }
}

project(":core") {
    apply plugin: "java-library"
    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        
    }
}
